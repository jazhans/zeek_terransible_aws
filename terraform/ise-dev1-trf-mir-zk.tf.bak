# Resources ingesting mirrored Traffic
resource "aws_ec2_traffic_mirror_target" "zeek_target" {
  provider             = aws.region-master
  description          = "VPC Tap for Zeek"
  network_interface_id = aws_instance.ise-dev1-ec2-zk-a.primary_network_interface_id
  tags = {
    Name = "VPC Tap for Zeek"
  }
}
resource "aws_ec2_traffic_mirror_filter" "zeek_filter" {
  provider    = aws.region-master
  description = "Zeek Mirror Filter - Allow All"
}
# Filter rules, all common ports to and fro
resource "aws_ec2_traffic_mirror_filter_rule" "zeek_outbound" {
  provider                 = aws.region-master
  description              = "Zeek Outbound Rule"
  traffic_mirror_filter_id = aws_ec2_traffic_mirror_filter.zeek_filter.id
  destination_cidr_block   = "0.0.0.0/0"
  source_cidr_block        = "0.0.0.0/0"
  rule_number              = 1
  rule_action              = "accept"
  traffic_direction        = "egress"
  protocol                 = 6
}
resource "aws_ec2_traffic_mirror_filter_rule" "zeek_inbound" {
  provider                 = aws.region-master
  description              = "Zeek Inbound Rule"
  traffic_mirror_filter_id = aws_ec2_traffic_mirror_filter.zeek_filter.id
  destination_cidr_block   = "0.0.0.0/0"
  source_cidr_block        = "0.0.0.0/0"
  rule_number              = 1
  rule_action              = "accept"
  traffic_direction        = "ingress"
  protocol                 = 6
  destination_port_range {
    from_port = 1
    to_port   = 65535
  }
  source_port_range {
    from_port = 1
    to_port   = 65535
  }
}
# Resources that send traffic to sensor
# Traffic mirroring uses VXLAN encapsulation and is on UDP port 4789
# command to verify VXLAN traffic is populating sudo tcpdump -i eth0 udp port 4789
resource "aws_ec2_traffic_mirror_session" "zeek_session" {
  provider                 = aws.region-master
  description              = "Zeek Mirror Session"
  traffic_mirror_filter_id = aws_ec2_traffic_mirror_filter.zeek_filter.id
  traffic_mirror_target_id = aws_ec2_traffic_mirror_target.zeek_target.id
  network_interface_id     = aws_instance.ise-dev1-ec2-web-a.primary_network_interface_id
  session_number           = 1
}
